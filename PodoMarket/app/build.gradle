plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'com.google.gms.google-services'
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.2"

    def localProperties = new Properties()
    localProperties.load(new FileInputStream(rootProject.file("local.properties")))

    defaultConfig {
        applicationId "com.android.example.podomarket"
        minSdkVersion 19
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"
        multiDexEnabled true

        buildConfigField "String", "PODO_SERVER_BASE_URL", '"https://api.podomarket.shop/"'

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        debug {
            storeFile rootProject.file('debug.jks')
            keyAlias localProperties['keyAlias']
            keyPassword localProperties['keyPassword']
            storePassword localProperties['storePassword']
        }
    }

    buildTypes {
        debug {
            manifestPlaceholders = [KAKAO_APP_KEY: localProperties['kakaoAppKey']]
            resValue "string", "KAKAO_APP_KEY", localProperties['kakaoAppKey']
            resValue "string", "GOOGLE_SERVER_CLIENT_ID", localProperties['googleServerClientId']
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        dataBinding true
    }
}

dependencies {

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation "androidx.viewpager2:viewpager2:1.0.0"
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.multidex:multidex:2.0.1'

    def navVersion = "2.3.2"
    // Navigation
    implementation "androidx.navigation:navigation-fragment-ktx:$navVersion"
    implementation "androidx.navigation:navigation-ui-ktx:$navVersion"

    //Timber
    implementation 'com.jakewharton.timber:timber:4.7.1'

    //Test
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    def koinVersion = '2.2.1'
    // Koin (Dependency Injection)
    implementation "org.koin:koin-core:$koinVersion"
    implementation "org.koin:koin-core-ext:$koinVersion"
    implementation "org.koin:koin-android:$koinVersion"
    implementation "org.koin:koin-android-scope:$koinVersion"
    implementation "org.koin:koin-android-viewmodel:$koinVersion"

    def retrofitVersion = '2.9.0'
    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava3:$retrofitVersion"
    implementation "com.google.code.gson:gson:2.8.6"

    def okhttp3Version = '4.7.2'
    // OkHTTP3
    implementation "com.squareup.okhttp3:okhttp:$okhttp3Version"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp3Version"

    // RxJava
    implementation 'io.reactivex.rxjava3:rxjava:3.0.3'
    implementation 'io.reactivex.rxjava3:rxandroid:3.0.0'

    // kakao Login
    implementation "com.kakao.sdk:v2-user-rx:2.2.0"
    implementation "com.kakao.sdk:v2-user:2.2.0"

    // Google Login
    implementation "com.google.android.gms:play-services-auth:19.0.0"

    // Import the Firebase BoM
    implementation platform('com.google.firebase:firebase-bom:26.2.0')

    // Add the dependency for the Firebase SDK for Google Analytics
    // When using the BoM, don't specify versions in Firebase dependencies
    implementation 'com.google.firebase:firebase-analytics-ktx'

    // Add the dependencies for any other desired Firebase products
    // https://firebase.google.com/docs/android/setup#available-libraries

    // Declare the dependency for the Realtime Database library
    // When using the BoM, you don't specify versions in Firebase library dependencies
    implementation 'com.google.firebase:firebase-database-ktx'

    // Glide (Image loader)
    implementation 'com.github.bumptech.glide:glide:4.11.0'

    //ted permission
    implementation "gun0912.ted:tedpermission:2.1.0"
}